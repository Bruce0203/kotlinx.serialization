import static KotlinVersion.isKotlinVersionAtLeast

/*
 * Copyright 2017-2021 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply from: rootProject.file("gradle/native-targets.gradle")
apply from: rootProject.file("gradle/configure-source-sets.gradle")

// disable kover tasks because there are no tests in the project
tasks.named("koverHtmlReport") {
    enabled = false
}
tasks.named("koverXmlReport") {
    enabled = false
}
tasks.named("koverVerify") {
    enabled = false
}

// Right now it is used for conditional support of kotlin 1.9.0 and 1.9.20+
def isNewWasmTargetEnabled = isKotlinVersionAtLeast(rootProject.properties["kotlin_version"], 1, 9, 20)

kotlin {

    sourceSets {
        commonMain {
            dependencies {
                api project(":kotlinx-serialization-core")
            }
        }
        jsWasmMain {
            dependsOn(sourceSets.commonMain)
        }
        jsMain {
            dependsOn(sourceSets.jsWasmMain)
        }
        if (isNewWasmTargetEnabled) {
            wasmJsMain {
                dependsOn(sourceSets.jsWasmMain)
            }
        } else {
            wasmMain {
                dependsOn(sourceSets.jsWasmMain)
            }
        }
    }
}

Java9Modularity.configureJava9ModuleInfo(project)

// This task should be disabled because of no need to build and publish intermediate JsWasm sourceset
tasks.whenTaskAdded { task ->
    if (task.name == 'compileJsWasmMainKotlinMetadata') {
        task.enabled = false
    }
}